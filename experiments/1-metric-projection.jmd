---
title: Example 1, Metric Projection
weave_options:
    fig_ext: .svg
---

```{julia}
using ProximalDistanceAlgorithms
using Random, Plots, DataFrames
include("plotutils.jl")
```

### Introduction

```{julia}
@doc metric_projection
```

### Example

```{julia}
nodes = 64
Random.seed!(5357)
_, Y = metric_example(64)
Y
```

```{julia}
function trivec(Y)
    n = size(Y, 1)
    y = zeros(binomial(n, 2))
    k = 0
    for j in 1:n, i in j+1:n
        y[k+=1] = Y[i,j]
    end
    return y
end

y = trivec(Y)
```

#### Setup

```{julia}
function solve(A, algorithm, maxiters, penalty; mu = 1.0)
    # use Nesterov acceleration unless we're running ADMM
    accel = algorithm isa ADMM ? Val(:none) : Val(:nesterov)

    if algorithm isa SteepestDescent
        println("Steepest Descent + Nesterov")

        # track loss, penalized objective, gradient, etc.
        history = initialize_history(maxiters+1)

        # warm-up
        println(" | warm-up:")
        print(" | ")
        @time metric_projection(algorithm, A,
            maxiters = 100,
            penalty = penalty,
            accel = accel,
            history = history)

        # real timing
        println(" | result:")
        print(" | ")
        history = initialize_history(maxiters+1)
        solution = @time metric_projection(algorithm, A,
            maxiters = maxiters,
            penalty = penalty,
            accel = accel,
            history = history)
    else
        algstr = algorithm isa MM ? "MM + Nesterov" : "ADMM"
        println(algstr)

        # track loss, penalized objective, gradient, etc.
        h1 = initialize_history(maxiters+1)
        h2 = initialize_history(maxiters+1)
        history = (lsqr = h1, cg = h2)

        # store solutions
        sol = []

        for (ls, h) in zip((Val(:LSQR), Val(:CG)), (h1, h2))
            lsstr = ls isa Val{:LSQR} ? "LSQR"  : "CG"
            println(" |")
            println(" | linear solver: $(lsstr)")

            # warm-up
            println(" | warm-up:")
            print(" | ")
            tmp = initialize_history(maxiters+1)
            @time metric_projection(algorithm, A,
                maxiters = 100,
                penalty = penalty,
                accel = accel,
                history = tmp,
                ls = ls,
                mu = mu)

            # real timing
            println(" | result:")
            print(" | ")
            s = @time metric_projection(algorithm, A,
                maxiters = maxiters,
                penalty = penalty,
                accel = accel,
                history = h,
                ls = ls,
                mu = mu)

            push!(sol, s)
        end

        solution = (lsqr=sol[1], cg=sol[2])
    end

    return solution, history
end
```

#### Fusion matrix

```{julia}
D = MetricFM(nodes); S = instantiate_fusion_matrix(D)
size(D)
```

```{julia}
unicodeplots()
spy(S)
```

```{julia}
spy(S'S)
```

#### Annealing schedules

```{julia}
penalty1(ρ, n) = min(1e6, 1.1 ^ floor(n/20))
penalty2(ρ, n) = min(1e6, 1.1 ^ floor(n/50))
gr(linewidth=2)

maxiters = 2000
xs = 1:maxiters
plot(xs, penalty1.(1, xs), label = "MM/SD")
plot!(xs, penalty2.(1, xs), label = "ADMM")
xlabel!("iteration")
ylabel!("rho")
```

#### MM

```{julia}
MM_sol, MM_trace = solve(Y, MM(), maxiters, penalty1)
plot_summary(MM_trace.lsqr)
```

```{julia}
plot_summary(MM_trace.cg)
```

#### Steepest Descent

```{julia}
SD_sol, SD_trace = solve(Y, SteepestDescent(), maxiters, penalty1)
plot_summary(SD_trace)
```

#### ADMM

```{julia}
ADMM_sol, ADMM_trace = solve(Y, ADMM(), maxiters, penalty2, mu = 7.5e-2)
plot_summary(ADMM_trace.lsqr)
```

```{julia}
plot_summary(ADMM_trace.cg)
```

### Summary


```{julia}
table_summary(MM_trace, SD_trace, ADMM_trace)
```

### Appendix

```{julia}
using Pkg; Pkg.status()
```

```{julia}
using InteractiveUtils; versioninfo()
```
